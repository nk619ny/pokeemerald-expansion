//.set LOCALID_MOVE_RELEARNER, 1

mapscripts FallarborTown_MoveRelearnersHouse_MapScripts {
	
}

const TempVar_HeartStoneDialogue = VAR_TEMP_0

script FallarborTown_MoveRelearnersHouse_EventScript_MoveRelearner {
	lockall
	applymovement(LOCALID_MOVE_RELEARNER, Common_Movement_FacePlayer)
	waitmovement(0)
	if (flag(FLAG_TEMP_1)) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_AskTeachMove)
	}
	msgbox(FallarborTown_MoveRelearnersHouse_Text_ImTheMoveTutor, MSGBOX_DEFAULT)
	setflag(FLAG_TEMP_1)
	goto(FallarborTown_MoveRelearnersHouse_EventScript_AskTeachMove)
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_AskTeachMove {
	checkitem(ITEM_HEART_SCALE)
	if (var(VAR_RESULT) == FALSE) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_ComeBackWithHeartScale)
	}
	msgbox(FallarborTown_MoveRelearnersHouse_Text_ThatsAHeartScaleWantMeToTeachMove, MSGBOX_YESNO)
	switch (var(VAR_RESULT)) {
		case NO:
			goto(FallarborTown_MoveRelearnersHouse_EventScript_ComeBackWithHeartScale)
	}
	goto(FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon)
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon {
	msgbox(FallarborTown_MoveRelearnersHouse_Text_TutorWhichMon, MSGBOX_DEFAULT)
	setmoverelearnerstate(MOVE_RELEARNER_LEVEL_UP_MOVES) // Specifically supposed to teach level up moves
	special(ChooseMonForMoveRelearner)
	waitstate
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_ComeBackWithHeartScale)
	}
	special(IsSelectedMonEgg)
	if (var(VAR_RESULT) == TRUE) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_CantTeachEgg)
	}
	if (var(VAR_0x8005) == 0) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_NoMoveToTeachMon)
	}
	goto(FallarborTown_MoveRelearnersHouse_EventScript_ChooseMove)
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_ChooseMove {
	msgbox(FallarborTown_MoveRelearnersHouse_Text_TeachWhichMove, MSGBOX_DEFAULT)
	special(TeachMoveRelearnerMove)
	waitstate
	if (var(VAR_0x8004) == 0) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon)
	}
	msgbox(FallarborTown_MoveRelearnersHouse_Text_HandedOverHeartScale, MSGBOX_DEFAULT)
	removeitem(ITEM_HEART_SCALE)
	goto(FallarborTown_MoveRelearnersHouse_EventScript_ComeBackWithHeartScale)
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_NoMoveToTeachMon {
	msgbox(FallarborTown_MoveRelearnersHouse_Text_DontHaveMoveToTeachPokemon, MSGBOX_DEFAULT)
	goto(FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon)
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_CantTeachEgg {
	msgbox(FallarborTown_MoveRelearnersHouse_Text_CantTeachEgg, MSGBOX_DEFAULT)
	goto(FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon)
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_ComeBackWithHeartScale {
	msgbox(FallarborTown_MoveRelearnersHouse_Text_ComeBackWithHeartScale, MSGBOX_DEFAULT)
	releaseall
	end
}

text FallarborTown_MoveRelearnersHouse_Text_HeartStoneExplained{
	format("Some Pokémon can only evolve based on their gender.\pHowever, a Heart Stone can be used to evolve Pokémon of the opposite gender.\pI can turn Heart Scales into Heart Stones you can use to trigger these evolutions.")
}

text FallarborTown_MoveRelearnersHouse_Text_ImTheMoveTutor {
	format("I'm the MOVE TUTOR.\pI know all the moves that Pokémon learn--every one of them--and I can teach Pokémon those moves.\pI can teach a move to a Pokémon of yours if you'd like.\pI'll do it for a HEART SCALE. I'm collecting those now.")
}

text FallarborTown_MoveRelearnersHouse_Text_ThatsAHeartScaleWantMeToTeachMove {
	format("Oh! That's it! That's an honest to goodness HEART SCALE!\pLet me guess, you want me to teach a move?")
}

text FallarborTown_MoveRelearnersHouse_Text_TutorWhichMon {
	format("Which Pokémon needs tutoring?")
}

text FallarborTown_MoveRelearnersHouse_Text_TeachWhichMove {
	format("Which move should I teach?")
}

text FallarborTown_MoveRelearnersHouse_Text_DontHaveMoveToTeachPokemon {
	format("Sorry…\pIt doesn't appear as if I have any move I can teach that Pokémon.")
}

text FallarborTown_MoveRelearnersHouse_Text_HandedOverHeartScale {
	format("{PLAYER} handed over one HEART SCALE in exchange.")
}

text FallarborTown_MoveRelearnersHouse_Text_ComeBackWithHeartScale {
	format("If your Pokémon need to learn a move, come back with a HEART SCALE.")
}

text FallarborTown_MoveRelearnersHouse_Text_CantTeachEgg {
	format("Hunh? There isn't a single move that I can teach an EGG.")
}

script FallarborTown_MoveRelearnersHouse_EventScript_HeartScaleExchange {
	lockall
	faceplayer
	msgbox(FallarborTown_MoveRelearnersHouse_Text_HeartStoneExplained, MSGBOX_DEFAULT)
	checkitem(ITEM_HEART_SCALE)
	if (var(VAR_RESULT) == FALSE) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_NoHeartScale)
	}
	msgbox(FallarborTown_MoveRelearnersHouse_Text_ExchangeHeartScale, MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
			goto(FallarborTown_MoveRelearnersHouse_EventScript_DeclineExchange)
	}
	removeitem(ITEM_HEART_SCALE)
	giveitem(ITEM_HEART_STONE)
	//msgbox(FallarborTown_MoveRelearnersHouse_Text_ExchangeComplete, MSGBOX_DEFAULT)
	releaseall
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_NoHeartScale {
	msgbox(FallarborTown_MoveRelearnersHouse_Text_NoHeartScale, MSGBOX_DEFAULT)
	releaseall
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_DeclineExchange {
	msgbox(FallarborTown_MoveRelearnersHouse_Text_DeclineExchange, MSGBOX_DEFAULT)
	releaseall
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_HeartStoneFarewell {
	lock
	switch (var(TempVar_HeartStoneDialogue)) {
		case 0:
			msgbox(format("A"), MSGBOX_DEFAULT)
	}
	releaseall
	end
}

text FallarborTown_MoveRelearnersHouse_Text_ExchangeHeartScale {
	format("Would you like to exchange a HEART SCALE for a HEART STONE?")
}

text FallarborTown_MoveRelearnersHouse_Text_NoHeartScale {
	format("It looks like you don't have a Heart Scale.\pCome back when you do.")
}

text FallarborTown_MoveRelearnersHouse_Text_ExchangeComplete {
	format("{PLAYER} exchanged the HEART SCALE for a HEART STONE!")
}

text FallarborTown_MoveRelearnersHouse_Text_DeclineExchange {
	format("Come back if you change your mind.")
}
